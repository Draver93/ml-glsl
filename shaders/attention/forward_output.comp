#version 430
layout(local_size_x = 16, local_size_y = 16) in;

layout(std430) readonly buffer AttentionWeights { float weights[]; }; // [num_heads * seq_len, seq_len]
layout(std430) readonly buffer BufferV { float V[]; };                // [seq_len, model_dim]
layout(std430) writeonly buffer BufferOutput { float buffer_output[]; };     // [seq_len, model_dim]

uniform int seq_len;
uniform int head_dim;
uniform int num_heads;

void main() {
    uint output_seq_pos = gl_GlobalInvocationID.x;  // output sequence position
    uint output_dim_idx = gl_GlobalInvocationID.y;  // output dimension index
    
    if (output_seq_pos >= uint(seq_len) || output_dim_idx >= uint(num_heads * head_dim)) return;
    
    uint head_idx = output_dim_idx / uint(head_dim);
    uint head_dim_idx = output_dim_idx % uint(head_dim);
    
    float result = 0.0;
    for (int key_pos = 0; key_pos < seq_len; key_pos++) {
        uint attention_weight_idx = (head_idx * seq_len + output_seq_pos) * seq_len + key_pos;
        float attention_weight = weights[attention_weight_idx];
        
        uint v_col_major_idx = (head_idx * head_dim + head_dim_idx) * seq_len + key_pos;
        float v_val = V[v_col_major_idx];
        
        result += attention_weight * v_val;
    }
    
    uint output_col_major_idx = (head_idx * head_dim + head_dim_idx) * seq_len + output_seq_pos;
    buffer_output[output_col_major_idx] = result;
}