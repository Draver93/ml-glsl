#version 430
layout(local_size_x = 16, local_size_y = 16) in;

layout(std430) readonly buffer GradOutput { float grad_out[]; };             // [seq_len, head_dim]
layout(std430) readonly buffer CachedV { float cached_v[]; };                // [seq_len, head_dim]
layout(std430) readonly buffer CachedAttentionWeights { float cached_attn[]; }; // [seq_len, seq_len]

layout(std430) writeonly buffer GradAttentionWeights { float grad_attn[]; };  // [seq_len, seq_len]
layout(std430) writeonly buffer GradV { float grad_v[]; };                   // [seq_len, head_dim]

uniform int seq_len;
uniform int head_dim;

void main() {
    uint seq_pos_i = gl_GlobalInvocationID.x;  // first sequence position
    uint seq_pos_j = gl_GlobalInvocationID.y;  // second sequence position
    
    if (seq_pos_i < uint(seq_len) && seq_pos_j < uint(seq_len)) {
        float grad_weight = 0.0;
        for (int head_dim_idx = 0; head_dim_idx < head_dim; head_dim_idx++) {
            uint grad_out_col_major_idx = head_dim_idx * seq_len + seq_pos_i;
            uint cached_v_col_major_idx = head_dim_idx * seq_len + seq_pos_j;
            grad_weight += grad_out[grad_out_col_major_idx] * cached_v[cached_v_col_major_idx];
        }
        uint grad_attn_idx = seq_pos_i * seq_len + seq_pos_j;
        grad_attn[grad_attn_idx] = grad_weight;
    }
    
    if (seq_pos_i < uint(seq_len) && seq_pos_j < uint(head_dim)) {
        float grad_val = 0.0;
        for (int key_pos = 0; key_pos < seq_len; key_pos++) {
            uint cached_attn_idx = key_pos * seq_len + seq_pos_i;
            uint grad_out_col_major_idx = seq_pos_j * seq_len + key_pos;
            grad_val += cached_attn[cached_attn_idx] * grad_out[grad_out_col_major_idx];
        }
        uint grad_v_col_major_idx = seq_pos_j * seq_len + seq_pos_i;
        grad_v[grad_v_col_major_idx] = grad_val;
    }
}