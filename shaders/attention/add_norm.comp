#version 430

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

// Main input buffer (e.g., output of sublayer)
layout(std430) readonly buffer InputA { float input_a[]; }; // [seq_len, model_dim]
// Residual input buffer (e.g., skip connection)
layout(std430) readonly buffer InputB { float input_b[]; }; // [seq_len, model_dim]
// Learnable scale (gamma) and shift (beta) parameters
layout(std430) readonly buffer Gamma { float gamma[]; };    // [model_dim]
layout(std430) readonly buffer Beta { float beta[]; };      // [model_dim]
// Output buffer
layout(std430) writeonly buffer Output { float output_buffer[]; }; // [seq_len, model_dim]
layout(std430) readonly buffer PaddingMask { int padding_mask[]; };

uniform int seq_len;
uniform int model_dim;
uniform float epsilon;
uniform bool has_padding_mask = false;

void main() {
    uint seq_pos = gl_GlobalInvocationID.x;
    if (seq_pos >= uint(seq_len)) return;

    // Skip PAD tokens in normalization
    if (has_padding_mask && padding_mask[seq_pos] == 0) {
        for (int dim = 0; dim < model_dim; dim++) {
            uint col_major_idx = dim * seq_len + seq_pos;
            float val = input_a[col_major_idx] + input_b[col_major_idx];
            output_buffer[col_major_idx] = val;
        }
        return;
    }

    // Normalization for real tokens
    float mean = 0.0;
    float variance = 0.0;

    for (int dim = 0; dim < model_dim; dim++) {
        uint col_major_idx = dim * seq_len + seq_pos;
        float val = input_a[col_major_idx] + input_b[col_major_idx];
        mean += val;
    }
    mean /= float(model_dim);

    for (int dim = 0; dim < model_dim; dim++) {
        uint col_major_idx = dim * seq_len + seq_pos;
        float val = input_a[col_major_idx] + input_b[col_major_idx];
        float diff = val - mean;
        variance += diff * diff;
    }
    variance /= float(model_dim);
    float stddev = sqrt(variance + epsilon);

    for (int dim = 0; dim < model_dim; dim++) {
        uint col_major_idx = dim * seq_len + seq_pos;
        float val = input_a[col_major_idx] + input_b[col_major_idx];
        float norm = (val - mean) / stddev;
        output_buffer[col_major_idx] = gamma[dim] * norm + beta[dim];
    }
}